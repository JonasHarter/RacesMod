package SaltWave
	import Location2
	import Unit2
	import Vectors
	import AoE
	import Unit
	import TargetChecker
	import DummyCaster

	let WAVE_ID = 'A03J'
	let C_ID = 'A03K'
	let C_O = "cripple"
	let RANGE = 800.00
	let FINAL_AREA = 300.00
	TargetChecker TARGETS

	// Damage of the spell
	function DAMAGE( int abilityLevel) returns real
		return 50.0 * abilityLevel
		
	function MAX_DAMAGE( int abilityLevel) returns real
		return 450.0 * abilityLevel
		
	init
		CreateTrigger()
			..registerAnyUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT)
			..addAction(function actions)
			..addCondition(Condition(function conditions))
		TARGETS = new TargetChecker()
			..addRestriction(Restrictions.Ally)
			..addRestriction(Restrictions.Structure)

	function conditions() returns bool
		return GetSpellAbilityId() == WAVE_ID

	function actions()
		unit caster = GetTriggerUnit()
		int level = caster.getAbilityLevel(WAVE_ID)
		vec2 start = caster.getPos()
		vec2 target = GetSpellTargetLoc().asVec2(true)
		target = start.polarOffset(start.angleTo(target), RANGE)
		group targets = getUnitsInCone(start, target, FINAL_AREA / 2)
		let dmg = DAMAGE(level)
		for unit u in targets
			if TARGETS.checkRestriction(caster, u).result
				DummyCaster d = new DummyCaster(C_ID, C_O, caster.getOwner(), true)
				d.setLevel(level)
				d.castOnTarget(u)
				caster.damageTargetSpell(u, dmg)
		targets.destr()
			