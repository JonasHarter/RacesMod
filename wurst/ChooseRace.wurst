package ChooseRace
	import Dialog2
	import StartingPosition

	init
		CreateTrigger()
			..registerTimerEvent(0.1, false)
			..addAction(function buildDialog)
		CreateTrigger()
			..registerTimerEvent(1.0, false)
			..addAction(function showDialog)
			
	dialog raceDialog = DialogCreate()
	button array raceButtons
	
	function buildDialog()
		for int i = 0 to bj_MAX_PLAYERS
			if Player(i).isIngame()
				group units = GetUnitsOfPlayerAll(Player(i))
				for unit u in units
					u.remove()
				units.destr()
		raceDialog.clear()
		raceDialog.setHeading("Choose your race")
		raceButtons[0] = raceDialog.addButton("|cff0042FF Human |r")
		raceButtons[1] = raceDialog.addButton("|cffFF0303 Orc |r")
		raceButtons[2] = raceDialog.addButton("|cff1CE6B9 Nightelf |r")
		raceButtons[3] = raceDialog.addButton("|cff540081 Undead |r")
		raceButtons[4] = raceDialog.addButton("|cffFFFC01 Goblin |r")
		raceButtons[5] = raceDialog.addButton("|cffFEBA0E Demon |r")
		raceButtons[6] = raceDialog.addButton("|cff20C000 Naga |r")
		CreateTrigger()
			..registerDialogEvent(raceDialog)
			..addAction(function dialogClick)
		
	function showDialog()
		for int i = 0 to bj_MAX_PLAYERS
			if Player(i).isIngame()
				raceDialog.show(Player(i))
					
				
	function dialogClick()
		let p = GetTriggerPlayer()
		let b = GetClickedButton()
		Races r = Races.Human
		if(b == raceButtons[1])
			r = Races.Orc
		else if(b == raceButtons[2])
			r = Races.NightElf
		else if(b == raceButtons[3])
			r = Races.Undead
		else if(b == raceButtons[4])
			r = Races.Goblin
		else if(b == raceButtons[5])
			r = Races.Demon
		else if(b == raceButtons[6])
			r = Races.Naga
		createStartingUnits(p, r)