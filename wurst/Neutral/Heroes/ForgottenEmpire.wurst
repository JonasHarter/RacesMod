package ForgottenEmpire
	import Basics
	import ClosureTimers

	constant UNIT_ID = 'o00C'
	string EFFECT_STR_1 = "Objects\\Spawnmodels\\Undead\\ImpaleTargetDust\\ImpaleTargetDust.mdl"
	string EFFECT_STR_2 = "Objects\\Spawnmodels\\Undead\\UCancelDeath\\UCancelDeath.mdl"
	constant real DURATION = 15.0
	//constant real FINAL_FLYHEIGHT = -270.0
	constant real RATE_FLYHEIGHT = 100.0
	constant real TIME_FLYHEIGHT = 2.7

	init
		Preload(EFFECT_STR_1)
		Preload(EFFECT_STR_2)
		CreateTrigger()
			..registerRectEnterEventSource(GetPlayableMapRect())
			..addCondition(Condition(function condition))
			..addAction(function create)

	function condition() returns bool
		return GetTriggerUnit().getTypeId() == UNIT_ID

	function create()
		let u = GetTriggerUnit()
		let pos = u.getPos()
		addEffect(EFFECT_STR_1, pos).destr()
		u.addAbility(HEIGHT_ENABLER)
		u.removeAbility(HEIGHT_ENABLER)
		u.setFlyHeight(0.0, RATE_FLYHEIGHT)
		u.pause()
		doAfter(TIME_FLYHEIGHT, () -> u.unpause())
		doAfter(DURATION, () -> die(u))
		
	//setFlyHeight doesn't take negative values :(
	function die(unit u)
		let pos = u.getPos()
		addEffect(EFFECT_STR_2, pos).destr()
		u.pause()
		u.kill()
		u.remove()