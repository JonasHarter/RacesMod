package SpikeNova
	import Vectors2
	import Location2
	import TargetChecker
	import LinkedList
	import InstantDummyCaster
	import Unit2
	import Group2

	let AB_ID = 'A024'
	let SS_ID = 'A025'
	string SS_O = "shadowstrike"
	let RANGE = 600.0
	let maxAmountPerTarget = 3
	TargetChecker check

	function targetAmount(integer level) returns integer
		return 3 + level * 2

	function damage(integer level) returns real
		return 30.0 + level * 10
		
	init
		check = new TargetChecker()
			..addRestriction(Restrictions.Ally)
			..addRestriction(Restrictions.Structure)
			..addRestriction(Restrictions.MagicImmune)
			..addRestriction(Restrictions.Invincible)
		CreateTrigger()
			..registerAnyUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT)
			..addCondition(Condition(function conditions ))
			..addAction(function actions)
			
	function conditions() returns bool
		return GetSpellAbilityId() == AB_ID

	function actions()
		let caster = GetTriggerUnit()
		let level = caster.getAbilityLevel(AB_ID)
		location loc = caster.getPos().asLoc()
		group allTargets = GetUnitsInRangeOfLocAll(RANGE, loc)
		loc.remove()
		if allTargets.getSize() == 0
			allTargets.destr()
			return
		// Get targets
		LinkedList<unit> validTargets = new LinkedList<unit>()
		LinkedList<int> validTargetsCounter = new LinkedList<int>()
		for u in allTargets
			if check.checkRestriction(caster, u).result
				validTargets.add(u)
				validTargetsCounter.add(0)
		allTargets.destr()
		if validTargets.size() == 0
			destroy validTargets
			destroy validTargetsCounter
			return
		// Assign amounts
		let amount = targetAmount(level)
		int i = 1
		while i <= amount and i <= validTargets.size() * maxAmountPerTarget
			let rand = GetRandomInt(0, validTargets.size() - 1)
			let cur = validTargetsCounter.get(rand)
			if cur < maxAmountPerTarget
				validTargetsCounter.set(rand, cur + 1)
				i++
		// Damage
		for j = 0 to validTargets.size() - 1
			for k = 1 to validTargetsCounter.get(j)
				let target = validTargets.get(j)
				InstantDummyCaster.castTarget(caster.getOwner(), SS_ID, level, SS_O, target, caster.getPos())
				caster.damageTargetSpell(target, damage(level))
		destroy validTargets
		destroy validTargetsCounter
		
	
