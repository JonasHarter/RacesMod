package PsychoticHaze
	import Location2
	import TargetChecker
	import ClosureTimers

	let SPELL_ID = 'A01F'
	let SPELL_R = 260.0
	let EFFECT_R = 520.0
	let EFFECT = "Goblin\\MindFog.mdl"
	let TARGETS = new TargetChecker()
			..addRestriction(Restrictions.Structure)

	init
		CreateTrigger()
			..registerAnyUnitEvent( EVENT_PLAYER_UNIT_SPELL_EFFECT )
			..addCondition( Condition( function conditions ) )
			..addAction( function actions )
		Preload(EFFECT)

	function conditions() returns bool
		return GetSpellAbilityId() == SPELL_ID
		
	function actions()
		let caster = GetTriggerUnit()
		location loc = GetSpellTargetLoc()
		let g = GetUnitsInRangeOfLocAll(SPELL_R, loc)
		let pos = loc.asVec2(true)
		for u in g
			if TARGETS.checkRestriction(caster, u).result
				let tempPos = pos.add(GetRandomReal(-EFFECT_R, EFFECT_R), GetRandomReal(-EFFECT_R, EFFECT_R))
				u.issuePointOrder("move", tempPos)
		g.destr()
		let e = addEffect(EFFECT, pos)
		doAfter(2.0, () -> e.destr())

